# Pruefe auf vorhandenen Identifier und Standort HSLU
unless exists(identifier)
  reject()
end
unless any_match(sample_location.*, 'HSLU')
  reject()
end
# Datum vorbereiten
substring('date.*',0,4)
# Schreibe MARC-Record
marc_add('LDR', ' ', 04043nrd#a2200301uu#4500)
# Identifier und Kontrolfelder
do identity()
  replace_all('_identifier', '[^0-9]', '')
  prepend('_identifier', 'matarch_')
  marc_add('001', a, $._identifier)
end
do identity()
  marc_add('007', ' ', 'cr')
  set_field('cf008_1', '170707s')
  copy_field('date.0', 'cf008_2')
  set_field('cf008_3', '####sz#nnn#f########rnzxx|d')
  paste(cf008_full,cf008_1,cf008_2,cf008_3,join_char:'')
  marc_add('008', ' ', $.cf008_full)
end
# Weitere MARC-Felder.
marc_add('110', ind1, '2', ind2, ' ', a, $.creator, e, 'Sammler', 4, 'col')
marc_add('245', ind1, '1', ind2, '0', a, $.title, b, $.subtitle, c, $.creator)
marc_add('246', ind1, '1', ind2, '3', a, $.synonym)
marc_add('264', ind1, ' ', ind2, '1', a, '[ZÃ¼rich]', b, '[Material-Archiv]', c, $.date)
# remove line break from description, do description field
replace_all('description.*', '\n', ' ')
marc_add('500', a, $.description)
# 'brand' ist 'Handelsnamen'
prepend('brand.*', 'Handelsnamen: ')
marc_add('500', a, $.brand)
# iterate over partial_subject to create single subject field for each
do list(path:partial_subject, var:s)
  marc_add('653', ind1, '1', ind2, '0', a, $.s)
end
# Links to website and thumbnail
marc_add('856', ind1, '4', ind2, '9', u, $.identifier, z, 'Weitere Informationen via materialarchiv.ch')
marc_add('856', ind1, '4', ind2, '0', u, $.thumbnail, z, 'Cover')
# Erstelle Exemplar (nur HSLU-Standorte)
if any_equal(sample_location.*, 'HSLU T+A Campus Horw')
  marc_add('AVA', ind1, ' ', ind2, ' ', b, 'LUHTA', j, 'TABIB', d, '-')
end
if any_equal(sample_location.*, 'HSLU D+K Luzern Sentimatt')
  marc_add('AVA', ind1, ' ', ind2, ' ', b, 'LUHGK', j, 'HGFRE', d, '-')
end
